buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.14.4'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
    }
}

// Tasks to build and test the android library
apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        minSdkVersion 4
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFile 'proguard.txt'
        }
    }

    lintOptions {
        disable 'DefaultLocale'
    }
}

// Generate .jar files when building archives
android.libraryVariants.all { variant ->
    def task = project.tasks.create "jar${variant.buildType.name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

// Tasks to release the library to maven central
apply from: 'https://raw.github.com/chrisbanes/gradle-mvn-push/master/gradle-mvn-push.gradle'
